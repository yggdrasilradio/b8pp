	' THE MONTY HALL PROBLEM: A Simulation
	' by Rick Adams

	dim d$(3)

	' User input
	print "Monty Hall problem"
	print
	print "Are we going to switch (Y/N)";
	input a$
	print
	print "How many repetitions";
	input n

	' Let's play the game repeatedly
	randomize
	for i = 1 to n
		gosub 1000
	next i

	' Grand total
	print
	print "Wins:"; w
	print "Losses:"; l

	stop

	' Load up the doors with goats
1000	d$(1) = "G"
	d$(2) = "G"
	d$(3) = "G"

	' Put a car behind a random door
	d$(int(3 * rnd(0)) + 1) = "C"

	' Your guess
	print
	g = int(3 * rnd(0)) + 1
	print "You guess door #"; g

	' Monty always opens another door
	' Door has to have a goat behind it and not be the one you picked
1020	d = int(3 * rnd(0)) + 1
	if d$(d) = "C" then 1020
	if d = g then 1020

	' How about it, pal?
	print "Monty opens door #"; d; "to reveal a goat."

	' Do we switch?
	if a$ = "N" then 1040

	' Let's switch!
	gosub 2000
	print "You decide to switch to door #"; f
	goto 1050

	' Let's stick with our original guess
1040	f = g
	print "You stick with door #"; f

	' How'd that work out for you?
1050	if d$(f) = "G" then 1060

	print "Beep beep! You win! It's a car!"
	w = w + 1
	return

1060	print "Too baaaaad! It's a goat!"
	l = l + 1
	return

	' Let's switch!
2000	for j = 1 to 3
	if j = g then 2010
	if j = d then 2010
	f = j
2010	next j
	return

	end
